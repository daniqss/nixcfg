{
  hostname,
  username,
  pkgs,
  lib,
  config,
  ...
}: let
  cfg = config.graphical.desktops;

  cursor = "Bibata-Modern-Classic-Hyprcursor";
  cursorSize = 24;
  cursorPackage = pkgs.bibata-hyprcursor;

  nuclearOption = pkgs.writeShellScriptBin "nuclear-option" ''
    sleep 2
    kill $(pidof xdg-desktop-portal-gnome)
    sleep 1
    ${pkgs.xdg-desktop-portal-gnome}/libexec/xdg-desktop-portal-gnome &
  '';
in {
  imports = [
    ./keybinds.nix
    ./rules.nix
    ./hypridle.nix
    ./hyprlock.nix
    # ./hyprsunset.nix
    ./hyprpolkitagent.nix
  ];

  config = lib.mkIf (cfg.desktop == "hyprland") {
    xdg.dataFile."icons/${cursor}".source = "${cursorPackage}/share/icons/${cursor}";

    wayland.windowManager.hyprland = {
      enable = true;
      systemd.enable = !cfg.uwsm.enable;

      package = null;
      portalPackage = null;

      settings = {
        env = [
          "HYPRCURSOR_THEME, ${cursor}"
          "HYPRCURSOR_SIZE, ${toString cursorSize}"
          "HYPRSHOT_DIR, /home/${username}/Pictures/screenshots"

          "NIXOS_OZONE_WL, 1"
          "QT_QPA_PLATFORMTHEME, qt6ct"
        ];

        exec-once = [
          "hyprctl setcursor ${cursor} ${toString cursorSize}"
          "wl-paste --type text --watch cliphist store"
          "wl-paste --type image --watch cliphist store"

          "${lib.getExe nuclearOption}"
        ];

        # generated by matugen
        source = "${config.xdg.configHome}/hypr/colors.conf";

        # host specific config
        monitor = cfg.monitorToDesktopConfig cfg.monitors;

        # For all categories, see https://wiki.hyprland.org/Configuring/Variables/
        input = {
          kb_layout =
            if hostname == "stoneward"
            then "us, es"
            else if hostname == "windrunner"
            then "es, us"
            else null;

          follow_mouse = 1;
          focus_on_close = 1;

          touchpad = {
            natural_scroll = true;
            disable_while_typing = false;
            scroll_factor = 1.4;
          };

          sensitivity = -0.6; # -1.0 - 1.0, 0 means no modification.
        };

        general = {
          gaps_in = 4;
          gaps_out = 8;

          border_size = 3;
          "col.active_border" = "$primary $secondary $tertiary 10deg";
          "col.inactive_border" = "$surface_bright";

          layout = "dwindle";
          allow_tearing = true;

          snap = {
            enabled = true;
            window_gap = 15;
            monitor_gap = 25;
            border_overlap = true;
          };
        };

        decoration = {
          rounding = 10;
          rounding_power = 2;

          blur = {
            enabled = false;
            size = 8;
            passes = 2;

            vibrancy = 0.1696;
            brightness = 0.5;

            special = true;
          };

          shadow = {
            enabled = true;
            range = 4;
            render_power = 3;
            color = "$shadow";
          };
        };

        animations = {
          enabled = true;
          bezier = "overshot, 0.13, 0.99, 0.29, 1.0";

          animation = [
            "windows, 1, 4, overshot, slide"

            "border, 1, 10, default"
            "fade, 1, 7, default"

            "workspaces, 0"
            "workspacesIn, 0"
            "workspacesOut, 0"
            "specialWorkspace, 1, 6, overshot, slidevert"
          ];
        };

        dwindle = {
          force_split = 0;
          special_scale_factor = 0.8;
          split_width_multiplier = 1.0;
          use_active_for_splits = true;
          pseudotile = true;
          preserve_split = true;
        };

        master = {
          special_scale_factor = 0.8;
        };

        misc = {
          disable_splash_rendering = true;
          disable_hyprland_logo = true;
          always_follow_on_dnd = false;
          layers_hog_keyboard_focus = true;
          animate_manual_resizes = false;
          enable_swallow = true;
          focus_on_activate = true;
          vfr = 1;
        };

        device = lib.mkIf (hostname == "windrunner") {
          name = "elan071a:00-04f3:30fd-touchpad";
          sensitivity = 0.1;
        };
      };
    };
  };
}
